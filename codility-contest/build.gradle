apply plugin: "java"
apply plugin: "maven"
apply plugin: "war"

group = "myorg"
version = 1.0

repositories {
    mavenCentral()
}

sourceSets.all { set ->
    def jarTask = task("${set.name}Jar", type: Jar) {
        baseName = baseName + "-$set.name"
        from set.output
    }

    artifacts {
        archives jarTask
    }
}

sourceSets {
    api
    impl
}

dependencies {
    
    //spring
    compile group: 'org.springframework', name: 'spring-web', version:'4.3.0.RELEASE'
    compile group: 'org.springframework', name: 'spring-webmvc', version:'4.3.0.RELEASE'
    
    //spring security
    compile "org.springframework.security:spring-security-core:4.2.3.RELEASE"
    compile "org.springframework.security:spring-security-config:4.2.3.RELEASE"
    compile "org.springframework.security:spring-security-web:4.2.3.RELEASE"
    compile "org.springframework.security:spring-security-ldap:4.2.3.RELEASE"
    compile "org.springframework.security:spring-security-taglibs:4.2.3.RELEASE"
        
    //logger
    compile group: 'org.slf4j', name: 'slf4j-api', version:'1.6.1'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version:'1.6.1'
    compile group: 'org.slf4j', name: 'jcl-over-slf4j', version:'1.6.1'
    
    //BeanUtils
    compile group: 'commons-beanutils', name: 'commons-beanutils', version: '1.8.3'
    
    apiCompile 'commons-codec:commons-codec:1.5'

    implCompile sourceSets.api.output
    implCompile 'commons-lang:commons-lang:2.6'

    testCompile 'junit:junit:4.9'
    testCompile sourceSets.api.output
    testCompile sourceSets.impl.output
    runtime configurations.apiRuntime
    runtime configurations.implRuntime
}

jar {
    from sourceSets.api.output
    from sourceSets.impl.output
}

war {
	archiveName = "codility-contest.war"
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri("${buildDir}/repo"))

            addFilter("main") { artifact, file -> artifact.name == project.name }
            ["api", "impl"].each { type ->
                addFilter(type) { artifact, file -> artifact.name.endsWith("-$type") }
                
                // We now have to map our configurations to the correct maven scope for each pom
                ["compile", "runtime"].each { scope ->
                    configuration = configurations[type + scope.capitalize()]
                    ["main", type].each { pomName ->
                        pom(pomName).scopeMappings.addMapping 1, configuration, scope
                    }
                }
            }

        }
    }
}
